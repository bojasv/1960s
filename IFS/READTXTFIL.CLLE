PGM

DCL &DS *CHAR 30
DCL &LIB *CHAR STG(*DEFINED) LEN(10) DEFVAR(&DS 1)
DCL &FIL *CHAR STG(*DEFINED) LEN(10) DEFVAR(&DS 11)
DCL &MBR *CHAR STG(*DEFINED) LEN(10) DEFVAR(&DS 21)

DCL &LOOP *LGL VALUE('1')
DCL &POS_A *UINT
DCL &POS_B *UINT
DCL &LEN *UINT
DCLF IFSTXTFILE ALWVARLEN(*YES)

DLTF QTEMP/IFSTXTFILE
MONMSG CPF0000
DLTF QTEMP/QPFSRC
MONMSG CPF0000

CHGJOB CCSID(37)
RUNSQL SQL('CREATE TABLE QTEMP.IFSTXTFILE (LINE) AS +
            (SELECT CAST(LINE AS CHAR(30)) FROM TABLE +
              (QSYS2.IFS_READ(''/home/OJASVA/SYNCDTA_MissingMBRs.txt''))) +
            WITH DATA') COMMIT(*NC)

/*EDT0303 - 240 bytes is max size supported by PDM*/
CRTSRCPF QTEMP/QPFSRC 240
MONMSG CPF7302
/* MONMSG CPF5813 */
/* File Already Exists*/

DOWHILE &LOOP
  RCVF
  MONMSG CPF0864 () LEAVE

  CALLSUBR PARSESTR
  CALLSUBR ACTION
ENDDO

/* Parse *LIB-PFSRC-SRCMBR */
SUBR PARSESTR
  CHGVAR &POS_A (%SCAN('-' &LINE)-1)
  CHGVAR &LIB (%SST(&LINE 1 &POS_A))
  CHGVAR &POS_A (&POS_A+2)
  CHGVAR &POS_B (%SCAN('-' &LINE &POS_A)+1)
  CHGVAR &LEN (&POS_B-&POS_A-1)
  CHGVAR &FIL (%SST(&LINE &POS_A &LEN))
  CHGVAR &POS_B (&POS_B)
  CHGVAR &MBR (%SST(&LINE &POS_B 10))
ENDSUBR

/* Specify command(s) to be looped-thru all the IFS file entries */
SUBR ACTION
  CPYF &LIB/&FIL QTEMP/QPFSRC &MBR &MBR *ADD FMTOPT(*MAP)
ENDSUBR

ENDPGM
